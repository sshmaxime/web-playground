set dotenv-load
set dotenv-required

DIST := "dist/"

ANVIL_DEFAULT_SENDER := "0xf39Fd6e51aad88F6F4ce6aB8827279cffFb92266"
ANVIL_DEFAULT_SENDER_PRIVATE_KEY := "0xac0974bec39a17e36ba4a6b4d238ff944bacb478cbed5efcae784d7bf4f2ff80"
ANVIL_DEFAULT_RPC_URL := "127.0.0.1:8545"

ANVIL_PARAM_BROADCAST := " --broadcast "
ANVIL_PARAM_RPC_URL := " --rpc-url "
ANVIL_PARAM_SENDER := " --sender "
ANVIL_PARAM_SENDER_PRIVATE_KEY := " --private-key "

ANVIL_DEFAULT_PARAMS := ANVIL_PARAM_BROADCAST + \
                        ANVIL_PARAM_RPC_URL + ANVIL_DEFAULT_RPC_URL + \
                        ANVIL_PARAM_SENDER + ANVIL_DEFAULT_SENDER + \
                        ANVIL_PARAM_SENDER_PRIVATE_KEY + ANVIL_DEFAULT_SENDER_PRIVATE_KEY

STORE_CONTRACT := "script/Store.s.sol"
export STORE_CONTRACT_SALT := env_var("STORE_CONTRACT_SALT")

# Installation.
install:
    forge install foundry-rs/forge-std@v1.9.3 --no-commit
    forge install OpenZeppelin/openzeppelin-foundry-upgrades@v0.3.6 --no-commit
    forge install OpenZeppelin/openzeppelin-contracts-upgradeable@v5.0.2 --no-commit

# Build.
build: generate_abi
    forge build

# Deployments.
deploy-anvil:
    forge script {{ STORE_CONTRACT }} {{ ANVIL_DEFAULT_PARAMS }}

# Test.
test:
    forge test

##############
### Utils. ###
##############

[private]
generate_abi: generate_abi_json generate_abi_ts

[private]
generate_abi_json:
    mkdir -p {{ DIST }}
    forge inspect src/Store.sol:Store abi > {{ DIST }}StoreAbi.json
    forge inspect src/Drop.sol:Drop abi > {{ DIST }}DropAbi.json

[private]
generate_abi_ts:
    mkdir -p {{ DIST }}
    echo "const StoreAbi = $(forge inspect src/Store.sol:Store abi) as const;\n\nexport { StoreAbi };" > {{ DIST }}StoreAbi.ts
    echo "const DropAbi = $(forge inspect src/Drop.sol:Drop abi) as const;\n\nexport { DropAbi };" > {{ DIST }}DropAbi.ts
